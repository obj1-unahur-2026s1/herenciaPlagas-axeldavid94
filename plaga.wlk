import elementos.*
class Plaga {
    var poblacion 

    method transmiteEnfermedad() = poblacion >= 10 and self.condicionAdicional()

    method condicionAdicional()

    method nivelDeDanio()

    method atacar(unElemento){
        unElemento.recibirAtaqueDe(self)
        poblacion = poblacion * 1.1
    }
}
class Cucarachas inherits Plaga {
    var pesoPromedio

    override method nivelDeDanio() = poblacion * 0.5

    override method condicionAdicional() = pesoPromedio >= 10

    override method atacar(unElemento){
        super(unElemento)
        pesoPromedio = pesoPromedio + 2
    }
}
class Mosquito inherits Plaga{
    override method nivelDeDanio() = poblacion
    override method condicionAdicional() = poblacion % 3 == 0
}
class Pulgas inherits Plaga{
    override method nivelDeDanio() = poblacion * 2
    override method condicionAdicional() = true
}
class Garrapatas inherits Pulgas {
    override method atacar(unElemento){
        unElemento.recibirAtaqueDe(self)
        poblacion = poblacion * 1.2
    }
}